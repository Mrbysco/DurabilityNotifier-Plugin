plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "3.0.0"
}

group = 'com.mrbysco'
version = "${version}"
base {
    archivesName = "${mod_name}"
}
description = "A paper plugin that allows clients to use Armor Poser to pose armor stands"

java {
    // Configure the java toolchain. This allows gradle to auto-provision JDK 21 on systems that only have JDK 11 installed for example.
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

repositories {
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paper_version}"
}

tasks {
    compileJava {
        options.release = 21
    }
    runServer {
        minecraftVersion("${mc_version}")
    }
}

processResources {
    def props = [version: version]
    getInputs().properties(props)
    filteringCharset("UTF-8")
    filesMatching("plugin.yml") {
        expand(props)
    }
}